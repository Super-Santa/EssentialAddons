plugins {
	id "maven-publish"
	id "fabric-loom" version "1.0-SNAPSHOT"
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id "io.github.juuxel.loom-quiltflower" version "1.7.0"
	id "com.replaymod.preprocess" version "897c4096e2"
	id 'me.modmuss50.mod-publish-plugin' version '0.3.4'
}

int mcVersion = 1
preprocess {
	mcVersion = vars.get()["MC"]
	tabIndentation = true
}

repositories {
	mavenCentral()
	maven {
		url "https://masa.dy.fi/maven"
	}
	maven {
		url "https://jitpack.io"
	}
	maven {
		url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		// Fabric permissions
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

archivesBaseName = project.archives_base_name
version = project.minecraft_version + "-" + project.mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "carpet:fabric-carpet:${project.carpet_core_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// modImplementation "com.github.CaffeineMC:lithium-fabric:${project.lithium_version}"

	include modImplementation("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")

	// Account authentication at runtime
	modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.0.0"

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		def map = [
			"version"             : project.mod_version,
			"carpet_dependency"   : project.carpet_dependency,
			"minecraft_dependency": project.minecraft_dependency
		]
		expand map
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

publishMods {
	file = remapJar.archiveFile
	changelog = """
	Fixed a bug that caused /lagspike to crash in singleplayer environments
	"""
	type = STABLE
	modLoaders.add("fabric")

	displayName = "EssentialAddons ${minecraft_version} v${mod_version}"

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_API_KEY")
		projectId = "3WQaouc1"
		minecraftVersions.add(minecraft_version)

		requires {
			id = "P7dR8mSH"
		}
		requires {
			id = "TQTTVgYE"
		}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
		}
	}
}